plugins {
  id 'java'
  id 'maven'
  id 'signing'
}

apply from: 'gradle/google-java-format.gradle'

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.25'
  compile 'com.google.guava:guava:20.0'

  testCompile 'org.slf4j:slf4j-simple:1.7.25'
  testCompile 'junit:junit:4.12'
  testCompile "com.google.truth:truth:0.32"
}

group = "io.ytcode"
version = "1.2.1"

sourceCompatibility = 1.7
targetCompatibility = 1.7

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

def username = hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('ossrhUsername')
def password = hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('ossrhPassword')

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: username, password: password)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: username, password: password)
      }

      pom.project {
        name 'reflect'
        packaging 'jar'
        description 'Java reflection utilities'
        url 'https://github.com/wangyuntao/reflect'

        scm {
          connection 'scm:git:git://github.com/wangyuntao/reflect.git'
          developerConnection 'scm:git:ssh://github.com:wangyuntao/reflect.git'
          url 'https://github.com/wangyuntao/reflect'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'wangyuntao'
            name 'wangyuntao'
            email 'wyt.daily@gmail.com'
          }
        }
      }
    }
  }
}